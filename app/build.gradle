apply plugin: 'com.android.application'
apply plugin: 'wire'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'io.fabric'

android {

  compileSdkVersion rootProject.compileSdkVersion
  buildToolsVersion rootProject.buildToolsVersion

  sourceSets {
    main {
      wire {
        srcDir "src/main/proto"
      }
    }
  }

  signingConfigs {
    debug {
      if (rootProject.file('debug.properties').exists()) {
        final def props = new Properties()
        props.load(new FileInputStream(rootProject.file('debug.properties')))

        storeFile file(props['DEBUG_STORE_FILE'])
        storePassword props['DEBUG_STORE_PASSWORD']
        keyAlias props['DEBUG_KEY_ALIAS']
        keyPassword props['DEBUG_KEY_PASSWORD']
      }
    }
    release {
      if (rootProject.file('release.properties').exists()) {
        final def props = new Properties()
        props.load(new FileInputStream(rootProject.file('release.properties')))

        storeFile file(props['RELEASE_STORE_FILE'])
        storePassword props['RELEASE_STORE_PASSWORD']
        keyAlias props['RELEASE_KEY_ALIAS']
        keyPassword props['RELEASE_KEY_PASSWORD']
        v2SigningEnabled true
      }
    }
  }

  defaultConfig {
    applicationId 'com.tevinjeffrey.rutgersct'
    minSdkVersion 19
    targetSdkVersion rootProject.targetSdkVersion
    versionCode rootProject.buildVersion
    versionName '1.0.10.1'
    signingConfig signingConfigs.release
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    vectorDrawables {
      useSupportLibrary = true
    }

    resConfigs 'en'

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
      }
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      shrinkResources true
      minifyEnabled true
      versionNameSuffix 'R'
      multiDexEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    debug {
      proguardFiles 'proguard-rules.pro'
      versionNameSuffix 'D'
      applicationIdSuffix '.debug'
      multiDexEnabled true
      // Crashlytics will generate a new build id for every build. Disable it
      ext.alwaysUpdateBuildId = false
    }
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/rxjava.properties'
  }

  lintOptions {
    abortOnError false
    checkReleaseBuilds false
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  if (project.hasProperty('devBuild')) {
    aaptOptions.cruncherEnabled = false
  }
}


wire {
  android = false
}

dependencies {
  implementation project(':ratemyprofessors')

  testImplementation 'org.mockito:mockito-core:1.10.19'
  testImplementation 'junit:junit:' + rootProject.junitVersion

  implementation('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
    transitive = true
  }

  implementation "com.android.support:design:" + rootProject.supportLibVersion
  implementation "com.android.support:support-v4:" + rootProject.supportLibVersion
  implementation "com.android.support:appcompat-v7:" + rootProject.supportLibVersion
  implementation "com.android.support:recyclerview-v7:" + rootProject.supportLibVersion
  implementation "com.android.support:recyclerview-v7:" + rootProject.supportLibVersion
  implementation "com.android.support:support-annotations:" + rootProject.supportLibVersion

  implementation 'com.android.support:multidex:1.0.2'

  implementation 'com.google.firebase:firebase-core:' + rootProject.playServicesVersion
  implementation 'com.google.firebase:firebase-messaging:' + rootProject.playServicesVersion
  implementation 'com.google.firebase:firebase-perf:' + rootProject.playServicesVersion
  implementation 'com.google.firebase:firebase-crash:' + rootProject.playServicesVersion

  implementation "io.reactivex.rxjava2:rxjava:" + rootProject.rxJava2Version
  implementation 'io.reactivex.rxjava2:rxandroid:' + rootProject.rxAndroidVersion

  implementation 'com.squareup.retrofit2:retrofit:' + rootProject.retrofit2Version
  implementation 'com.squareup.retrofit2:converter-moshi:' + rootProject.retrofit2Version
  implementation 'com.squareup.retrofit2:adapter-rxjava2:' + rootProject.retrofit2Version
  implementation 'com.squareup.okhttp3:logging-interceptor:' + rootProject.okhttp3Version
  implementation 'com.squareup.retrofit2:converter-wire:' + rootProject.retrofit2Version
  implementation 'com.squareup.wire:wire-runtime:2.2.0'

  implementation 'com.google.dagger:dagger:' + rootProject.daggerVersion
  implementation 'com.google.dagger:dagger-android-support:' + rootProject.daggerVersion
  kapt 'com.google.dagger:dagger-compiler:' + rootProject.daggerVersion
  kapt 'com.google.dagger:dagger-android-processor:' + rootProject.daggerVersion

  implementation 'android.arch.lifecycle:extensions:' + rootProject.archLifecycleVersion
  implementation 'android.arch.persistence.room:runtime:' + rootProject.archRoomVersion
  implementation 'android.arch.persistence.room:rxjava2:' + rootProject.archRoomVersion
  kapt "android.arch.lifecycle:compiler:" + rootProject.archLifecycleVersion
  kapt "android.arch.persistence.room:compiler:" + rootProject.archRoomVersion

  implementation 'com.dlazaro66.wheelindicatorview:WheelIndicatorView:1.0.0'
  implementation 'com.github.afollestad.material-dialogs:core:0.9.4.3@aar'
  implementation 'com.jenzz:materialpreference:1.3'
  implementation 'com.github.paolorotolo:appintro:3.4.0'
  implementation 'com.jonathanfinerty.once:once:1.2.1'
  implementation 'com.jakewharton.timber:timber:4.6.0'
  implementation 'com.squareup.moshi:moshi:1.5.0'
  implementation 'com.facebook.stetho:stetho:' + rootProject.stetho
  implementation 'com.facebook.stetho:stetho-okhttp3:' + rootProject.stetho
  implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

  implementation 'frankiesardo:icepick:3.2.0'
  kapt 'frankiesardo:icepick-processor:3.2.0'

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta3'

  androidTestCompile 'com.android.support.test:runner:' + rootProject.runnerVersion
  androidTestCompile 'com.android.support.test:rules:' + rootProject.runnerVersion
  androidTestCompile 'com.android.support.test.espresso:espresso-core:' + rootProject.espressoVersion
  androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2') {
    exclude group: 'com.android.support', module: 'appcompat'
    exclude group: 'com.android.support', module: 'support-v4'
    exclude module: 'recyclerview-v7'
  }
  androidTestCompile "com.android.support:support-annotations:" + rootProject.supportLibVersion
  androidTestCompile "com.android.support:design:" + rootProject.supportLibVersion
  androidTestCompile "com.android.support:appcompat-v7:" + rootProject.supportLibVersion

}

apply plugin: 'com.google.gms.google-services'