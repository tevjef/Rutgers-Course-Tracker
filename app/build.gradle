apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {

  compileSdkVersion rootProject.compileSdkVersion
  buildToolsVersion rootProject.buildToolsVersion

  signingConfigs {
    debug {
      if (rootProject.file('debug.properties').exists()) {
        final def props = new Properties()
        props.load(new FileInputStream(rootProject.file('debug.properties')))

        storeFile file(props['DEBUG_STORE_FILE'])
        storePassword props['DEBUG_STORE_PASSWORD']
        keyAlias props['DEBUG_KEY_ALIAS']
        keyPassword props['DEBUG_KEY_PASSWORD']
      }
    }
    release {
      if (rootProject.file('release.properties').exists()) {
        final def props = new Properties()
        props.load(new FileInputStream(rootProject.file('release.properties')))

        storeFile file(props['RELEASE_STORE_FILE'])
        storePassword props['RELEASE_STORE_PASSWORD']
        keyAlias props['RELEASE_KEY_ALIAS']
        keyPassword props['RELEASE_KEY_PASSWORD']
        v2SigningEnabled true
      }
    }
  }

  defaultConfig {
    applicationId 'com.tevinjeffrey.rutgersct'
    minSdkVersion 19
    targetSdkVersion rootProject.targetSdkVersion
    versionCode 33
    versionName '1.0.7.1'
    signingConfig signingConfigs.release
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    vectorDrawables {
      useSupportLibrary = true
    }

    resConfigs 'en'

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation":
                         "$projectDir/schemas".toString()]
      }
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    release {
      debuggable true
      signingConfig signingConfigs.release
      shrinkResources true
      minifyEnabled true
      versionNameSuffix 'R'
      multiDexEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    debug {
      proguardFiles 'proguard-rules.pro'
      versionNameSuffix 'D'
      applicationIdSuffix '.debug'
      multiDexEnabled true
      // Crashlytics will generate a new build id for every build. Disable it
      ext.alwaysUpdateBuildId = false
    }
  }

  packagingOptions {
    exclude 'LICENSE.txt'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/rxjava.properties'
  }

  lintOptions {
    abortOnError false
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  if (project.hasProperty('devBuild')) {
    aaptOptions.cruncherEnabled = false
  }
}

dependencies {
  compile project(':ratemyprofessors')

  androidTestCompile 'com.android.support.test:runner:' + rootProject.runnerVersion
  androidTestCompile 'com.android.support.test:rules:' + rootProject.runnerVersion
  androidTestCompile 'com.android.support.test.espresso:espresso-core:' + rootProject.espressoVersion
  androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2') {
    exclude group: 'com.android.support', module: 'appcompat'
    exclude group: 'com.android.support', module: 'support-v4'
    exclude module: 'recyclerview-v7'
  }
  androidTestCompile "com.android.support:support-annotations:" + rootProject.supportLibVersion
  androidTestCompile "com.android.support:design:" + rootProject.supportLibVersion
  androidTestCompile "com.android.support:appcompat-v7:" + rootProject.supportLibVersion

  testCompile 'org.mockito:mockito-core:1.10.19'
  testCompile 'junit:junit:' + rootProject.junitVersion

  compile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
    transitive = true
  }

  compile "com.android.support:design:" + rootProject.supportLibVersion
  compile "com.android.support:support-v4:" + rootProject.supportLibVersion
  compile "com.android.support:appcompat-v7:" + rootProject.supportLibVersion
  compile "com.android.support:recyclerview-v7:" + rootProject.supportLibVersion
  compile "com.android.support:recyclerview-v7:" + rootProject.supportLibVersion
  compile "com.android.support:support-annotations:" + rootProject.supportLibVersion
  compile 'com.android.support:multidex:1.0.2'

  compile 'com.google.firebase:firebase-core:' + rootProject.playServicesVersion
  compile 'com.google.firebase:firebase-messaging:' + rootProject.playServicesVersion

  compile "io.reactivex.rxjava2:rxjava:" + rootProject.rxJava2Version
  compile 'io.reactivex.rxjava2:rxandroid:' + rootProject.rxAndroidVersion

  compile 'com.squareup.retrofit2:retrofit:' + rootProject.retrofit2Version
  compile 'com.squareup.retrofit2:converter-gson:' + rootProject.retrofit2Version
  compile 'com.squareup.retrofit2:adapter-rxjava2:' + rootProject.retrofit2Version
  compile 'com.squareup.okhttp3:logging-interceptor:' + rootProject.okhttp3Version
  compile 'com.squareup.retrofit2:converter-wire:' + rootProject.retrofit2Version
  compile 'com.squareup.wire:wire-runtime:2.2.0'

  compile 'com.google.dagger:dagger:' + rootProject.daggerVersion
  compile 'com.google.dagger:dagger-android-support:' + rootProject.daggerVersion
  kapt 'com.google.dagger:dagger-compiler:' + rootProject.daggerVersion
  kapt 'com.google.dagger:dagger-android-processor:' + rootProject.daggerVersion

  compile 'android.arch.lifecycle:extensions:' + rootProject.archLifecycleVersion
  compile 'android.arch.persistence.room:runtime:' + rootProject.archRoomVersion
  compile 'android.arch.persistence.room:rxjava2:' + rootProject.archRoomVersion
  kapt "android.arch.lifecycle:compiler:" + rootProject.archLifecycleVersion
  kapt "android.arch.persistence.room:compiler:" + rootProject.archRoomVersion

  compile 'com.dlazaro66.wheelindicatorview:WheelIndicatorView:1.0.0'
  compile 'com.github.afollestad.material-dialogs:core:0.9.4.3@aar'
  compile 'com.orhanobut:hawk:2.0.1'
  compile 'com.jenzz:materialpreference:1.3'
  compile 'com.github.paolorotolo:appintro:3.4.0'
  compile 'com.jonathanfinerty.once:once:1.2.1'
  compile 'com.github.satyan:sugar:1.5'
  compile 'com.jakewharton.timber:timber:4.6.0'
  compile 'com.google.code.gson:gson:2.8.2'
  compile 'com.facebook.stetho:stetho:' + rootProject.stetho
  compile 'com.facebook.stetho:stetho-okhttp3:' + rootProject.stetho

  compile 'frankiesardo:icepick:3.2.0'
  kapt 'frankiesardo:icepick-processor:3.2.0'

  compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'
repositories {
  mavenCentral()
}
